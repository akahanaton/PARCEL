from snakemake.utils import R
from os.path import join
import csv

# simulate a bash login shell, see https://bitbucket.org/johanneskoester/snakemake/wiki/FAQ
shell.executable("/bin/bash")
#
# # "unofficial bash strict mode" http://www.redsymbol.net/articles/unofficial-bash-strict-mode/
shell.prefix("source ~/.bashrc; set -euo pipefail;")
#
#

# Globals ---------------------------------------------------------------------

# get the seqbatch : libid pairs for given comparebatch id
LIBIDS = []
SEQBATCHS = []
SEQDIR = []
SEQFILES = []
TREATMENTS = []
sampleinfo = open(config["SAMPLEINFO"],"r",newline='')
for line in sampleinfo:
	allelems = line.strip().split("\t");
	if allelems[6] == config["COMPAREBATCH"]:
		LIBIDS.append(allelems[1])
		SEQBATCHS.append(allelems[4])
		SEQDIR.append(allelems[8])
		SEQFILES.append(allelems[8] + allelems[7])
		if allelems[2] != config["CONTROL"]:
			TREATMENTS.append(allelems[2])

TREATMENTS = list(set(TREATMENTS))

#print(LIBIDS)
#print(SEQBATCHS)

# get condition and id pair
CONDITIONS = {}
sampleinfo = open(config["SAMPLEINFO"],"r",newline='')
for line in sampleinfo:
	CONDITIONS[line.split("\t")[1]] = line.split("\t")[2]


###################################################################################
# for calling differential sites
###################################################################################

OUTPUTDIRCOV = {
 "AUTOBATCH": {
    "T": "Batch",
    "F": "NoBatch"
 },
 "VSALL": {
    "T": "vsAll",
    "F": "onlyControl"
 },
}
# the merged coverage files
#MERGEDCOVDIR = RESULTDIR + "/"

#RESULTDIR = RESULTDIR + "/"

##################################################################################
#     Directories
##################################################################################
mybatch = config["COMPAREBATCH"]
RESULTROOT = config["RESULTROOT"]
RESULTDIR = config["RESULTROOT"] + "/" + "PARCEL/" + config["REFTYPE"] + "/" + mybatch + "/"
TRIMSEQDIR = config["RESULTROOT"] + "/" + "trimmedFastq/"
MAPRESULTDIR = config["RESULTROOT"] + "/" + "mappedResult/" + config["REFTYPE"] + "/"# the reftype could be genome, transcriptome or longesttranscriptome and so on.
COVERAGEDIR = config["RESULTROOT"] + "/" + "coverageInfo/" + config["REFTYPE"] + "/"
BEDGRAPHDIR = config["RESULTROOT"] + "/" + "bedgraphs/" + config["REFTYPE"] + "/" + mybatch + "/"
QUALITYDIR = config["RESULTROOT"] + "/" + "qualityCheck/" + mybatch + "/"
#DIFFREGIONS = RESULTDIR + mybatch + "diffRegions/"


def get_bedgraph_files(wc):
	return [ BEDGRAPHDIR + "/" + CONDITIONS[LIBIDS[k]] + '_{}'.format(LIBIDS[k]) + '_{}_nor.bedgraph.gz'.format(SEQBATCHS[k]) for k in range(len(LIBIDS))]

rule final:
	input: 
		QUALITYDIR + "/" + "processingSummary.xls",
		expand(RESULTDIR + "/" + "combined_{treatment}_output2_wfilters.txt",treatment = TREATMENTS),
		expand(RESULTDIR + "/" + "combined_{treatment}_covinfo.xls",treatment = TREATMENTS),
		#expand(RESULTDIR + "combined_{treatment}_output2_wfilters.bed",treatment = TREATMENTS),
		#expand(RESULTDIR + "{treatment}_covplot/sigRegion.xls",treatment = TREATMENTS),
		#expand(RESULTDIR + "{treatment}_covplot/sigRegion.highlightSeq.xls",treatment = TREATMENTS),
		get_bedgraph_files

rule trimAdaptor:
	input: 
		config["INPUTDIR"] + "{seqbatch}/" + "{id}.fastq.gz"
	output: 
		trimseq = TRIMSEQDIR + "{seqbatch}/" + "{id}.trim.fastq.gz",
		trimlog = TRIMSEQDIR + "{seqbatch}/" + "read.trim.{id}.log",
		trimsum = TRIMSEQDIR + "{seqbatch}/" + "read.trim.{id}.log.sum"
	params:
		adp1=config["ADAPTER"]["A"], adp2=config["ADAPTER"]["B"]

	log: TRIMSEQDIR + "{seqbatch}/" + "/read.trim.{id}.err"
	threads: 16
	shell:
		"""
			bash {config[CUTADPT]} {input} {output.trimseq} {params.adp1} {params.adp2} {threads} {output.trimlog}
			ID={wildcards.id} perl {config[PARSE_CUT_ADT]} {output.trimlog} > {output.trimsum}
		"""


rule collectAdpTrimLog:
	input:
		expand(TRIMSEQDIR + "{seqbatch}/" + 'read.trim.{id}.log.sum', zip, id=LIBIDS, seqbatch=SEQBATCHS)
	output:
		TRIMSEQDIR + config["COMPAREBATCH"] + "/" + "trimSummary.txt"
	shell:
		"""
			cat {input} > {output}
		"""

rule mapToGenome:
	input:
		rules.trimAdaptor.output.trimseq
	output:
		bamresult = MAPRESULTDIR + "{seqbatch}/" + "{id}.trim.fastq.genome_mapping_best.sort.bam",
		bamresultTmp = temp(MAPRESULTDIR + "{seqbatch}/" + "{id}.trim.fastq.genome_mapping_best.bam"),
		bamindex = MAPRESULTDIR + "{seqbatch}/" + "{id}.trim.fastq.genome_mapping_best.sort.bam.bai",
		maplog = MAPRESULTDIR + "{seqbatch}/" + "{id}.trim.fastq.genome_mapping.log",
		logsum = MAPRESULTDIR + "{seqbatch}/" + "{id}.trim.fastq.genome_mapping.summary"
		
	params:
		seqbatch = lambda wildcards: wildcards.seqbatch,
		mismatch = config["MISMATCH"],
		mapmodel = config["MAPMODEL"],
		genome = config["REFTRANSCRIPT"]
	threads: 12
	shell:
		"""
			MISMATCH={params.mismatch} sh {config[MAP_WAPPER]} {input} {threads} {params.genome} {output.maplog} {output.bamresultTmp}
			{config[SAMTOOLS]} sort -@ {threads} -T {wildcards.id} -m 1G -O BAM {output.bamresultTmp} > {output.bamresult}
			{config[SAMTOOLS]} index {output.bamresult}
			ID={wildcards.id} perl {config[SUM_MAPLOG]} {output.maplog} > {output.logsum}
		"""

rule bedgraphTrack:
	input:
		bamresult = rules.mapToGenome.output.bamresult
	output:
		BEDGRAPHDIR + "/" + "{condition}_{id}_{seqbatch}_nor.bedgraph.gz"
	params:
		condition = lambda wildcards: CONDITIONS[wildcards.id]
	shell:
		"""
			COVMIN=0 FIVEPOS=yes perl {config[TOBEDGRAPH]} {input.bamresult} {params.condition}_{wildcards.id}_nor {config[BEDGRAPH_SPE]} yes yes no | pigz -c -p 8 > {output}
		"""

rule collectMapLog:
	input:
		expand(MAPRESULTDIR + "{seqbatch}/" + '{id}.trim.fastq.genome_mapping.summary', zip, id=LIBIDS, seqbatch=SEQBATCHS)
	output:
		MAPRESULTDIR + config["COMPAREBATCH"] + "/" + "mapSummary.txt"
	shell:
		"""
			cat {input} > {output}
		"""
rule qualityCheck:
	input:
		sampleinfo = config["SAMPLEINFO"],
		trimlog = rules.collectAdpTrimLog.output,
		maplog = rules.collectMapLog.output
	output:
		proSum = QUALITYDIR  + "/" + "processingSummary.xls"
	params:
		outdir = QUALITYDIR , batch = config["COMPAREBATCH"]
	shell:
		"""
			Rscript {config[QUALCHECKSCRIPT]} {input.sampleinfo} {input.trimlog} {input.maplog} {params.batch} {params.outdir}
		"""

rule coverageCount:
	input:
		bamresult = rules.mapToGenome.output.bamresult
	output:
		COVERAGEDIR + "{seqbatch}/" + "{id}.cov.txt.gz"
	threads: 4
	params: numhit = config["NUMHIT"]
	shell:
		"""
			NUMHIT={params.numhit} sh {config[BAMTOCOV]} {input.bamresult} {wildcards.id} {config[TRANSCRIPTOMESIZE]} | pigz -p {threads} -c > {output} 
		"""

rule collectCovCount:
	input:
		expand(COVERAGEDIR + "{seqbatch}/" + "{id}.cov.txt.gz", zip, id=LIBIDS, seqbatch=SEQBATCHS)
	output:
		RESULTDIR + "allcov.txt.gz"
	threads: 8
	shell:
		"""
			zcat {input} | pigz -p {threads} -c > {output}
		"""

rule reshapeCovCount:
	input:
		rules.collectCovCount.output
	output:
		RESULTDIR + "allcov.wide.min" + config["COVMIN"] + ".txt.gz"
	params:
		splitby = config["SPLITBY"], sampleinfo = config["SAMPLEINFO"],downprop = config["DOWNSAMPLEPROP"]
	threads: 1
	shell:
		"""
			Rscript {config[RESHAPETABLE]} -i {input} -o {output} -v V3 -f V1+V2~V4 --header F --filterbySum {config[COVMIN]} --splitBy {params.splitby} --sampleinfo {params.sampleinfo} --downSampleProp {params.downprop}
		"""



rule saveCovToRdata:
	input:
		rules.reshapeCovCount.output
	output:
		RESULTDIR + "/" + "combined_v1all.Rdata"
	threads: 8
	params: 
		outdir = RESULTDIR , sampleinfo = config["SAMPLEINFO"], batchid = config["COMPAREBATCH"],ismerge = config["ISMERGE"]
	shell:
		"""
			Rscript {config[MERGETAB]} --args {params.outdir} {input} {params.sampleinfo} {params.batchid} {output} {params.ismerge}
		"""

rule calldiffsites:
	input:
		covfile = rules.saveCovToRdata.output 
	output: 
		pvaluefile = RESULTDIR + "/" + "etTable_{treatment}.Rdata",
		covfile = RESULTDIR + "/" + "covinfo_{treatment}.Rdata"
	params:
		outdir = rules.saveCovToRdata.params.outdir, mincov = config["COVMIN"], treatment = lambda wildcards: wildcards.treatment, sampleinfo = config["SAMPLEINFO"], ismerge = config["ISMERGE"],
		sffile = RESULTDIR + "/" + "edgeR_{treatment}_sf.Rdata", batchid = config["COMPAREBATCH"], control = config["CONTROL"], vsall = config["VSALL"]
	threads: 1
	shell:
		"""
			Rscript {config[RUNEDGER]} --args {params.outdir} {params.mincov} {params.treatment} {params.control} {params.vsall} {params.sampleinfo} {input.covfile} {params.sffile} {params.batchid} {params.ismerge}
		"""

rule getCandidateRegions:
	input:
		pvaluefile = rules.calldiffsites.output.pvaluefile,
		covfile = rules.calldiffsites.output.covfile
	output:
		RESULTDIR + "/" + "fastq2_{treatment}_output10.Rdata"
	params:
		mincov = config["COVMIN"], treatment = lambda wildcards: wildcards.treatment, outdir = rules.saveCovToRdata.params.outdir,
		sffile = rules.calldiffsites.params.sffile
	shell:
		"""
			Rscript {config[REGIONEVALUE]} --args {params.mincov} {params.treatment} {params.outdir} {input.covfile}
		"""
		

rule filterCandidates:
	input:
		diffsitefile = rules.getCandidateRegions.output,
		covfile = rules.calldiffsites.output.covfile
	output:
		filtercands = RESULTDIR + "/" + "combined_{treatment}_output2_wfilters.txt",
	params:
		mincov = config["COVMIN"], treatment = lambda wildcards: wildcards.treatment, outdir = rules.saveCovToRdata.params.outdir, genestructure = config["GENESTRUC"],
		sffile = rules.calldiffsites.params.sffile, sampleinfo = config["SAMPLEINFO"], batchid = config["COMPAREBATCH"], ismerge = config["ISMERGE"]
	shell:
		"""
			Rscript {config[FILTERCANDS]} --args {params.mincov} {params.treatment} {params.outdir} {input.covfile} {params.sffile} {params.genestructure} {params.sampleinfo} {params.batchid} {params.ismerge}
		"""
rule extractCovInfo:
	input:
		filtercands = rules.filterCandidates.output.filtercands,
		covfile = rules.saveCovToRdata.output
	output:
		covoutput = RESULTDIR + "/" + "combined_{treatment}_covinfo.xls"
	params:
		sampleinfo = config["SAMPLEINFO"], treatment = lambda wildcards: wildcards.treatment, control = config["CONTROL"],batch = config["COMPAREBATCH"], 
		genome = config["REFTRANSCRIPTFASTA"], outdir = rules.filterCandidates.params.outdir, ismerge = config["ISMERGE"], vsall = config["VSALL"],extsize = 100


	shell:
		"""
			Rscript {config[EXTRACTCOVINFO]} -i {input.filtercands} -s {params.sampleinfo} -b {params.batch} -t {params.treatment} -c {params.control} --covfile {input.covfile} --vsall {params.vsall} --ismerge {params.ismerge} --extWinSize {params.extsize} --genome {params.genome} --outdir {params.outdir}
		"""
# rule convertToGenome:
#          input:
#                  rules.filterCandidates.output.filtercands
#          output:
#                  candbed = RESULTDIR + "/" + "combined_{treatment}_output2_wfilters.bed",
#                  candseq = temp(RESULTDIR + "/" + "combined_{treatment}_output2_wfilters.fa"),
#          params:
#                  refTranscript = config["REFTRANSCRIPTFASTA"], refGenome = config["GENOMEFASTA"]
#          shell:
#                  """
#                          Rscript {config[CONVERTTOBED]} -i {input} -s {output.candseq} -o {output.candbed} -r {params.refTranscript} -g {params.refGenome}
#                  """
#
# rule coveragePlot:
#         input:
#                 filtercands = rules.convertToGenome.output.candbed,
#                 siterawcov = config["RAWCOV"]
#
#         output:
#                 sigregion = RESULTDIR + "/{treatment}_covplot/sigRegion.xls",
#                 sigregionHighlight = RESULTDIR + "/{treatment}_covplot/sigRegion.highlightSeq.xls",
#                 sequences = RESULTDIR + "/{treatment}_covplot/sequences.fas",
#                 ribocov = RESULTDIR + "/{treatment}_covplot/covinfo.xls"
#
#         params:
#                 sampleinfo = config["SAMPLEINFO"], nrexonbed = config["GENEEXONBED"], treatment = lambda wildcards: wildcards.treatment, control = config["CONTROL"],
#                 topcov = config["TOPCOV"], batch = config["COMPAREBATCH"], autobatch = config["AUTOBATCH"], genome = config["GENOMEFASTA"], cdsbed = config["CDSBED"],
#                 outdir = rules.filterCandidates.params.outdir + "{treatment}_covplot/", ismerge = config["ISMERGE"]
#         threads: 16
#         shell:
#                 """
#                         Rscript {config[COVPLOT]} -o {params.outdir} -i {input.filtercands} -s {params.sampleinfo} -b {params.batch} -t {params.treatment} -c {params.control} --covfile {input.siterawcov} --genome {params.genome} --getFasta T --CDSBed {params.cdsbed} --genomeSize {config[GENOMESIZE]} --ismerge {params.ismerge}
#                         perl {config[HIGHLIGHTSEQ]} {output.sigregion} {output.sigregionHighlight}
#                 """
#
